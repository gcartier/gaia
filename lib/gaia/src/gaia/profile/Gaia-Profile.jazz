;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Gaia Profile
;;;


(module protected gaia.profile.Gaia-Profile jazz


(import (jazz.designer)
        (jazz.editor.jazz)
        (jazz.foreign)
        (jazz.graphic)
        (jazz.ide)
        (jazz.io)
        (jazz.jml)
        (jazz.library)
        (jazz.library.component)
        (jazz.log)
        (jazz.platform)
        (jazz.project)
        (jazz.runtime)
        (jazz.system)
        (jazz.tie)
        (jazz.ui)
        (jazz.ui.activity)
        (jazz.ui.clipboard)
        (jazz.ui.view)
        (jazz.website)
        (gaia)
        (time))


;;;
;;;; SIX
;;;


(define six-types-extra
  '(short
    long
    size_t
    constcharptr
    FILEptr
    unsignedint
    unsignedshort
    unsignedchar
    stdvectorchar
    vec3_t
    vec4_t
    ms3d_vertex_tptr
    ms3d_joint_t
    ms3d_joint_tptr
    ms3d_group_tptr
    ms3d_triangle_tptr
    ms3d_material_tptr
    
    FLT
    point3
    vector3
    plane3
    ray3
    primitive
    point3ref
    ray3ref
    CollisionList
    CollisionType))


(define init-six
  (let ((inited? #f))
    (lambda ()
      (unless inited?
        (for-each (lambda (type)
                    (six-types-set! (cons (cons type #f) (six-types))))
                  six-types-extra)
        (set! inited? #t)))))


(define (parse-infix str)
  (let ((comments (new-queue)))
    (define (add-comment str)
      (enqueue comments str))
    
    (define (make-six-readtable readtable)
      (readtable-comment-handler-set (readtable-start-syntax-set readtable 'six) add-comment))
    
    (init-six)
    (call-with-input-string str
      (lambda (input)
        (with-exception-catcher
          (lambda (e)
            (continuation-capture
              (lambda (c)
                (display-exception-in-context e c))))
          (lambda ()
            (let ((expressions (parameterize ((current-readtable (make-six-readtable (input-port-readtable input))))
                                 (read-source-all input))))
              (values (queue-list comments)
                      expressions))))))))


;;;
;;;; Profile
;;;


(class Gaia-Profile extends IDE-Profile
  
  
  ;;;
  ;;;; C -> Lisp
  ;;;
  
  
  (method override (test8)
    (let ((text (get-focus)))
      (if (is-not? text Text-View)
          (bell)
        (let ((str (get-selected-string~ text)))
          (receive (comments expressions) (parse-infix str)
            (debug comments)
            (debug expressions)
            @w
            (let ((output (open-output-string)))
              (for-each (lambda (expr)
                          (pp expr output))
                        expressions)
              (let ((lisp (butlast (get-output-string output))))
                (let ((start (get-start~ text)))
                  (replace-selection~ text (split-lines lisp))
                  @w
                  (tabulate-range~ text (new-range~ text start (get-end~ text)))))))))))
  
  
  (method override (test9)
    (let ((text (get-focus)))
      (if (is-not? text Text-View)
          (bell)
        (let ((str (get-selected-string~ text)))
          (let ((start (get-start~ text)))
            (replace-selection~ text (split-lines (c->lisp str)))
            @w
            (tabulate-range~ text (new-range~ text start (get-end~ text))))))))
  
  
  (method override (test0)
    (let ((text (get-focus)))
      (if (is-not? text Text-View)
          (bell)
        (let ((str (get-clipboard-text)))
          (if (not str)
              (bell)
            (let ((start (get-start~ text)))
              (replace-selection~ text (split-lines (c->lisp str)))
              @w
              (tabulate-range~ text (new-range~ text start (get-end~ text)))))))))
  
  
  (method (c->lisp str)
    (receive (comments expressions) (parse-infix str)
      (let ((expressions (parse-c comments expressions)))
        (let ((output (open-output-string)))
          (for-each (lambda (expr)
                      (pp expr output))
                    expressions)
          ;; remove pp trailing return
          (butlast (get-output-string output))))))
  
  
  (method (parse-c comments expressions)
    (define unary-translations
      '((six.!x  . not)
        (six.++x . unimplemented)
        (six.x++ . increase!)
        (six.--x . unimplemented)
        (six.x-- . decrease!)
        (six.~x  . bitwise-not)
        (six.*x  . unimplemented)
        (six.+x  . +)
        (six.-x  . -)))
    
    (define binary-translations
      '((six.x%y      . modulo)
        (six.x*y      . *)
        (six.x/y      . /)
        (six.x+y      . +)
        (six.x-y      . -)
        (six.x<y      . <)
        (six.x<=y     . <=)
        (six.x>y      . >)
        (six.x>=y     . >=)
        (six.x!=y     . /=)
        (six.x==y     . =)
        (six.x&y      . bitwise-and)
        (six.x^y      . bitwise-xor)
        (|six.x\|y|   . bitwise-ior)
        (six.x&&y     . and)
        (|six.x\|\|y| . or)
        (six.x+=y     . increase!)
        (six.x-=y     . decrease!)
        (six.x*=y     . multiply!)
        (six.x/=y     . divide!)))
    
    (define next-comment
      #f)
    
    (define next-comment-filepos
      #f)
    
    (define (next-comment!)
      (if (null? comments)
          (begin
            (set! next-comment #f)
            (set! next-comment-filepos #f))
        (set! next-comment (car comments))
        (set! next-comment-filepos (position->filepos (locat-position (source-locat next-comment))))
        (set! comments (cdr comments))))
    
    (define (parse-forms exprs)
      (let (loop (scan exprs))
        (if (null? scan)
            '()
          (let ((src (first scan)))
            (let ((parsed-forms
                    (let ((c0 (desourcify src)))
                      (cond ((eq? (expr-kind c0) 'six.define-variable)
                             (bind (kind var type size val) c0
                               `((let ((,(parse var) ,(typeify (parse type)) ,@(if val (list (parse val)) '())))
                                   ,@(loop (cdr scan))))))
                            (else
                             (cons (parse c0) (loop (cdr scan))))))))
              (if (and next-comment-filepos
                       (let ((pos (and (source? src) (position->filepos (locat-position (source-locat src))))))
                         (and pos
                              (or (> (filepos-line pos) (filepos-line next-comment-filepos))
                                  (> (filepos-col pos) (filepos-col next-comment-filepos))))))
                  (prog1 (cons (list 'YO (desourcify next-comment)) parsed-forms)
                    (next-comment!))
                parsed-forms))))))
    
    (define (parse expr)
      (let ((kind (expr-kind expr)))
        (let ((unary (assq kind unary-translations)))
          (if unary
              (bind (kind x) expr
                `(,(cdr unary) ,(parse x)))
            (let ((binary (assq kind binary-translations)))
              (if binary
                  (bind (kind x y) expr
                    `(,(cdr binary) ,(parse x) ,(parse y)))
                (case kind
                  ((six.define-procedure)
                   (bind (kind name (kind type parameters body)) expr
                     `(define (,(parse name)) ,@(parse-body body))))
                  ((six.identifier)
                   (cadr expr))
                  ((six.if)
                   (bind (kind test then . rest) expr
                     (if (null? rest)
                         `(when ,(parse test)
                            ,(parse then))
                       `(if ,(parse test)
                            ,(parse then)
                          ,(parse (car rest))))))
                  ((six.for)
                   (bind (kind expr1 expr2 expr3 statement) expr
                     (let ((expr1 (case (expr-kind expr1)
                                    ((six.define-variable)
                                     (bind (kind var type size val) expr1
                                       `(,(parse var) ,(typeify (parse type)) iterate (set! ,(parse var) ,(parse val)))))
                                    ((six.x=y)
                                     (bind (kind var val) expr1
                                       `(,(parse var) iterate (set! ,(parse var) ,(parse val)))))
                                    (else
                                     (error "Unable to parse code")))))
                       `(loop (for ,@expr1 ,(parse expr2) ,(parse expr3))
                              ,@(parse-compound statement)))))
                  ((six.dot)
                   (bind (kind obj member) expr
                     `(,(string->symbol (format "{s}~~" (parse member)))
                       ,(parse obj))))
                  ((six.arrow)
                   (bind (kind obj member) expr
                     `(,(string->symbol (format "{s}~~" (parse member)))
                       ,(parse obj))))
                  ((six.index)
                   (bind (kind vec index) expr
                     `(vector-ref ,(parse vec) ,(parse index))))
                  ((six.compound)
                   `(begin
                      ,@(parse-forms (cdr expr))))
                  ((six.call)
                   (if (eq? (expr-kind (cadr expr)) 'six.dot)
                       (let ((dot (cadr expr)))
                         (bind (kind obj member) dot
                           `(,(string->symbol (format "{s}~~" (parse member)))
                             ,(parse obj)
                             ,@(parse-list (cddr expr)))))
                     (map parse (cdr expr))))
                  ((six.break)
                   `(break))
                  ((six.continue)
                   `(continue))
                  ((six.return)
                   `(return ,(parse (cadr expr))))
                  ((six.x=y)
                   (bind (kind var val) expr
                     (case (expr-kind var)
                       ((six.index)
                        `(vector-set! ,(parse (cadr var)) ,(parse (caddr var)) ,(parse val)))
                       (else
                        `(set! ,(parse var) ,(parse val))))))
                  ((six.&x)
                   (parse (cadr expr)))
                  ((six.x<<y)
                   (bind (kind x y) expr
                     `(arithmetic-shift ,(parse x) ,(parse y))))
                  ((six.x>>y)
                   (bind (kind x y) expr
                     `(arithmetic-shift ,(parse x) (- ,(parse y)))))
                  ((six.literal)
                   (cadr expr))
                  (else
                   expr))))))))
    
    (define (parse-list lst)
      (map parse lst))
    
    (define (parse-body expr)
      (parse-forms (cdr expr)))
    
    (define (parse-compound expr)
      (parse-forms (cdr expr)))
    
    (define (typeify expr)
      (assert (symbol? expr))
      (string->symbol (format "<{s}>" expr)))
    
    (define (referenceify expr)
      (string->symbol (format "<reference{s}>" expr)))
    
    (define (pointerify expr)
      (string->symbol (format "<pointer{s}>" expr)))
    
    (define (expr-kind expr)
      (and (pair? expr)
           (car expr)))
    
    (next-comment!)
    (parse-forms expressions))
  
  
  ;;;
  ;;;; Live Coding
  ;;;
  
  
  (method override (test)
    (define (literal-range text)
      (let ((has-selection? (has-selection?~ text))
            (expl (new (explorer-class~ text) (get-model~ text) start: (get-start~ text) end: (get-end~ text))))
        (let (iter)
          (when (or has-selection? (outer-expr~ expl))
            (set! has-selection? #f)
            (let ((range (get-range~ expl)))
              (when (= (get-line~ (get-start~ range))
                       (get-line~ (get-end~ range)))
                (let ((literal-string (get-marked~ expl))
                      (literal (with-jazz-readtable
                                 (lambda ()
                                   (read-marked~ expl)))))
                  (cond ((or (integer? literal) (and (number? literal) (inexact? literal)))
                         (or (iter) (list literal-string literal range)))
                        ((is? literal Color)
                         (list literal-string literal range))
                        (else
                         (iter))))))))))
    
    (define (choose-literal text undoer atomic-preserved modified? literal-string literal range pos)
      (define (choose-number)
        (let* ((page-height 10)
               (width 100)
               (mid (fxround/ width 2))
               (precision (and (inexact? literal)
                               (let ((pos (find literal-string #\.)))
                                 (if (not pos)
                                     0
                                   (- (cardinality literal-string) pos 1)))))
               (factor (if (inexact? literal)
                           (* 10 (/ (expt 10. precision)))
                         50)))
          (define (scroll-proc scrollbar action line-height page-height thumb-pos orientation)
            (define (scroll-action)
              (define (scroll amount)
                (set-pos~ scrollbar (+ (get-pos~ scrollbar) amount) redraw?: #t))
              
              (case action
                ((line-up)     (scroll (- line-height)))
                ((line-down)   (scroll line-height))
                ((page-up)     (scroll (- page-height)))
                ((page-down)   (scroll page-height))
                ((thumb-track) (set-pos~ scrollbar thumb-pos redraw?: #t))))
            
            (scroll-action)
            (let ((pos (get-pos~ scrollbar)))
              (when (not modified?)
                (set-caret~ text (get-start~ range)))
              (let ((normalized (/ (cast <fl> (- pos mid)) 50)))
                (let ((str (if (inexact? literal)
                               (format "{r precision: :v}" (+ literal (* normalized factor)) (+ precision 1))
                             (->string (+ literal (inexact->exact (round (* normalized factor)))))))
                      (start (get-start~ text)))
                  (replace~ text range (list str))
                  (update-window~ (get-toplevel))
                  (eval-sel text)
                  (set! modified? #t)
                  (set! range (new Range$Cell$ start (get-start~ text)))
                  (set-caret~ text start)))))
          
          (let ((choose
                  (new Scrollbar
                    size: {Dimension 200 13}
                    orientation: 'horz
                    pos: mid
                    range: (new Range 0 (+ width page-height))
                    page-height: page-height
                    scroll-proc: scroll-proc
                    return-press-handler: (new Event-Handler execute: return-press)
                    escape-press-handler: (new Event-Handler execute: escape-press)
                    focus-lose-handler: (new Event-Handler execute: edition-ended))))
            (popup~ text choose (nu- pos {Dimension 100 25}))
            (set-pos~ choose mid)
            (acquire-focus~ choose))))
      
      (define (choose-color)
        (let ((choose
                (new Color-Choose
                  size: {Dimension 527 109}
                  background: {Color Dark-Gray}
                  color: literal
                  feedback: (lambda (color)
                              (when (not modified?)
                                (set-caret~ text (get-start~ range)))
                              (let ((str (->string color))
                                    (start (get-start~ text)))
                                (replace~ text range (list str))
                                (update-window~ (get-toplevel))
                                (eval-sel text)
                                (set! modified? #t)
                                (set! range (new Range$Cell$ start (get-start~ text)))
                                (set-caret~ text start)))
                  return-press-handler: (new Event-Handler execute: return-press)
                  escape-press-handler: (new Event-Handler execute: escape-press)
                  focus-lose-handler: (new Event-Handler execute: edition-ended))))
          (popup~ text choose (nu- pos {Dimension 304 125}))
          (acquire-focus~ choose)))
      
      (define (return-press evt)
        (end-atomic-undo~ undoer atomic-preserved)
        (set! undoer #f)
        (close-popups~ (get-toplevel)))
      
      (define (escape-press evt)
        (end-atomic-undo~ undoer atomic-preserved)
        (set! undoer #f)
        (when modified?
          (undo~ text)
          (set-caret~ text (get-start~ text))
          (eval-sel text))
        (close-popups~ (get-toplevel)))
      
      (define (edition-ended evt)
        (when undoer
          (end-atomic-undo~ undoer atomic-preserved)))
      
      (cond ((or (integer? literal) (and (number? literal) (inexact? literal)))
             (choose-number))
            ((is? literal Color)
             (choose-color))))
    
    (define (eval-sel text)
      (with-safe-explore
        (lambda ()
          (let ((range (current-declaration-range~ text)))
            (if (not range)
                (bell)
              (evaluate-range~ text range))))))
    
    (let ((text (get-focus)))
      (let ((info (literal-range text)))
        (if (not info)
            (begin
              (user-message "No literal found")
              (bell))
          (let ((undoer (get-undoer~ (get-model~ text))))
            (let ((atomic-preserved (start-atomic-undo~ undoer)))
              (bind (literal-string literal range) info
                (let* ((start (get-start~ range))
                       (end (get-end~ range))
                       (mid (fxround/ (+ (get-col~ start) (get-col~ end)) 2))
                       (pos (text->view~ text (new Cell (get-line~ start) mid))))
                  (choose-literal text undoer atomic-preserved #f literal-string literal range pos)))))))))
  
  
  ;;;
  ;;;; Trailing Whitespace
  ;;;
  
  
  @w
  (method override (test8)
    (let ((project (find-project~ (get-workbench) 'world:lib)))
      (let ((directory (get-parent~ (get-tag-reference~ (get-form~ project)))))
        (search-trailing-whitespace directory: directory))))))
